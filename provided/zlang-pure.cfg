PROGRAM   -> STMTS $
STATEMENT -> BRACESTMTS
           | DECLLIST sc
           | ASSIGN sc
           | IF
           | IFELSE
           | WHILE
           | EMIT sc
STMTS     -> STMTS STATEMENT
           | lambda
BRACESTMTS -> lbrace STMTS rbrace

DECLTYPE  -> const bool
           | bool
           | const int
           | int
           | string
           | const float
           | float
DECLID    -> id 
           | ASSIGN
DECLIDS   -> DECLID
           | DECLIDS comma DECLID
DECLLIST  -> DECLTYPE DECLIDS 

ASSIGN    -> id assign EXPR 
           | id assign ASSIGN

IF        -> if lparen BEXPR rparen STATEMENT
IFELSE    -> if lparen BEXPR rparen BRACESTMTS else STATEMENT
WHILE     -> while lparen BEXPR rparen STATEMENT

EXPR      -> BEXPR | AEXPR

BOOLS     -> lt | leq | eq | geq | gt
BEXPR     -> AEXPR BOOLS AEXPR

PLUS      -> plus | minus
TIMES     -> mult | div | mod
AEXPR     -> SUM
SUM       -> SUM PLUS PRODUCT
           | PRODUCT
PRODUCT   -> PRODUCT TIMES VALUE
           | VALUE
VALUE     -> lparen AEXPR rparen 
           | lparen BEXPR rparen
           | intval
           | floatval
           | stringval
           | id
           | UNARY
           | CAST
UNARY     -> PLUS VALUE
           | not VALUE
           | compl VALUE
CAST      -> bool lparen AEXPR rparen
           | int lparen AEXPR rparen
           | float lparen AEXPR rparen

EMIT      -> emit id AEXPR AEXPR
           | emit symtable

